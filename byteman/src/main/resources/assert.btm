RULE invoke create
CLASS Invoked
METHOD <init>()
AT RETURN
IF TRUE
DO
  traceln("<init>");
  link("expected", "expected", java.util.List.of("a", "b", "c", "z"));
ENDRULE

RULE invoke add
CLASS Invoked
METHOD add
AT RETURN
IF TRUE
DO
  link("items", $1.toString(), $1.toString());
ENDRULE

RULE invoke end
CLASS Invoked
METHOD end
AT RETURN
BIND
  names = linkNames("items");
IF TRUE
DO
  java.util.Collections.sort(names);
ENDRULE

RULE invoke end assert true
CLASS Invoked
METHOD end
AT RETURN
BIND
  names = linkNames("items");
  expected = linked("expected", "expected");
IF
  names.equals(expected)
DO
  traceln("Success");
  link("success", true);
ENDRULE

RULE invoke end assert false
CLASS Invoked
METHOD end
AT RETURN
BIND
  names = linkNames("items");
  maybeSuccess = linked("success");
  success : boolean = maybeSuccess != null ? maybeSuccess : false;
  expected = linked("expected", "expected");
IF
  !success
DO
  traceln("Failure: expected " + expected + " but got " + names);
ENDRULE

RULE new toString
CLASS New
METHOD toString(java.lang.StringBuilder)
AT RETURN
BIND
  aNew : org.example.byteman.qbicc.New = $this;
  sb = new java.lang.StringBuilder();
IF TRUE
DO
  $! = sb.append(("New(" + aNew.type.toString() + ")@" + Integer.toHexString(System.identityHashCode($this))).toCharArray());
ENDRULE
